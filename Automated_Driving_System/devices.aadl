


package aadlbook::devices


public

with aadlbook::icd;
with aadlbook::platform;
with emv2;
 
-----------------------------------
--  Camera and Object Detection  --
-----------------------------------
   
device camera
features
	picture : out data port aadlbook::icd::picture;
flows
	f0 : flow source picture;
properties 
	Period => 200ms;
	compute_execution_time => 20 ms .. 50 ms;
annex EMV2 {**
	use types 		ErrorLibrary;

	error propagations
		picture : out propagation {ItemOmission};
	flows
		ef0 : error source picture{ItemOmission};
	end propagations;

	properties
  		AIAG_VDA::DFMEA =>[	FailureDescription =>"Loss of picture from the camera";
				  			Function=>"Sends the raw picture to a software component";
							PC=>"NONE";
							Occurrence=>7;
							DC=>"NONE";
							Detection=>10;] applies to picture.itemomission; 				 											 	
		AIAG_VDA::Optimization=>([	OptPC=>"Automatic calibration of the camera";
		  							OptDC=>"Real-time monitoring";
									ResponsPerson=>"David";
									TargetCompletionData=>"2023/3/15";
									Status=>"in progress";
									OPTOccurrence=>5;
									OPTDetection=>6;]) applies to picture.itemomission;
	    emv2::occurrencedistribution => [ probabilityValue => 0.01e-4 ; Distribution => Poisson;] applies to picture.itemomission;
**};	
end camera;


device radar
features
	distance_estimate : out data port aadlbook::icd::obstacle_position.i;
flows
	f0 : flow source distance_estimate;
properties 
	Period => 10ms;
	compute_execution_time => 6 ms .. 8 ms;
annex EMV2 {**
	use types 		ErrorLibrary;

	error propagations
		distance_estimate : out propagation {ItemOmission,OutOfRange};
	flows
		ef0 : error source distance_estimate{ItemOmission,OutOfRange};
	end propagations;

	properties
  		AIAG_VDA::DFMEA =>[	FailureDescription =>"Loss of information from the Radar";
				  			Function=>"Detects an obstacle on the road";
							PC=>"NONE";
							Occurrence=>6;
							DC=>"NONE";
							Detection=>7;] applies to  distance_estimate.ItemOmission; 
  		AIAG_VDA::DFMEA =>[ FailureDescription =>"Invalid distance sent by the radar";
		  		  			Function=>"Detects an obstacle on the road";
							PC=>"NONE";
							Occurrence=>6;
							DC=>"NONE";
							Detection=>7;] applies to  distance_estimate.OutOfRange; 				 											 	
		AIAG_VDA::Optimization=>([	OptPC=>"Automatic calibration of the camera";
		  							OptDC=>"Real-time monitoring";
									ResponsPerson=>"David";
									TargetCompletionData=>"2023/3/15";
									Status=>"in progress";
									OPTOccurrence=>3;
									OPTDetection=>4;]) applies to distance_estimate.ItemOmission;			 											 	
		AIAG_VDA::Optimization=>([	OptPC=>"Data redundancy";
		  							OptDC=>"Fault diagnosis";
									ResponsPerson=>"David";
									TargetCompletionData=>"2023/3/15";
									Status=>"in progress";
									OPTOccurrence=>3;
									OPTDetection=>4;]) applies to distance_estimate.OutOfRange;	
		EMV2::OccurrenceDistribution => [ ProbabilityValue => 0.5e-5 ; Distribution => Poisson;] applies to ef0.ItemOmission;
		EMV2::OccurrenceDistribution => [ ProbabilityValue => 0.5e-5 ; Distribution => Poisson;] applies to ef0.OutOfRange;
**};
end radar;
 
---------------------
--  Speed Sensors  --
---------------------

device speed_sensor
features
	speed : out data port aadlbook::icd::speed;
flows
	f0 : flow source speed;
annex EMV2 {**
	use types 		ErrorLibrary;
	error propagations
		speed : out propagation {ItemOmission,OutOfRange};
	flows
		ef0 : error source speed{ItemOmission,OutOfRange};
	end propagations;

	properties
		EMV2::OccurrenceDistribution => [ ProbabilityValue => 0.5e-5 ; Distribution => Poisson;] applies to speed.ItemOmission;
		EMV2::OccurrenceDistribution => [ ProbabilityValue => 0.5e-5 ; Distribution => Poisson;] applies to speed.OutOfRange;
**};
end speed_sensor; 

device speed_wheel_sensor extends speed_sensor
properties
	Period => 10ms;
	compute_execution_time => 6 ms .. 9 ms;
annex EMV2 {**
	properties
  		AIAG_VDA::DFMEA =>[ FailureDescription =>"Invalid value from wheel sensor";
				  			Function=>"Indicates the vehicle speed";
				 			PC=>"NONE";
							Occurrence=>6;
							DC=>"NONE";
							Detection=>7;] applies to  speed.OutOfRange; 
  		AIAG_VDA::DFMEA =>[ FailureDescription =>"Loss of speed from wheel sensor";
		  		  			Function=>"Indicates the vehicle speed";
							PC=>"NONE";
							Occurrence=>6;
							DC=>"NONE";
							Detection=>7;] applies to  speed.ItemOmission;				 											 	
		AIAG_VDA::Optimization=>([	OptPC=>"Data redundancy";
		  							OptDC=>"Fault diagnosis";
									ResponsPerson=>"David";
									TargetCompletionData=>"2023/3/15";
									Status=>"in progress";
									OPTOccurrence=>3;
									OPTDetection=>4;]) applies to speed.OutOfRange;					 											 	
		AIAG_VDA::Optimization=>([	OptPC=>"Data redundancy";
		  							OptDC=>"Fault diagnosis";
									ResponsPerson=>"David";
									TargetCompletionData=>"2023/3/15";
									Status=>"in progress";
									OPTOccurrence=>3;
									OPTDetection=>4;]) applies to speed.ItemOmission;  
**};
end speed_wheel_sensor;


device speed_laser_sensor extends speed_sensor
properties
	Period => 10ms;
	compute_execution_time => 6 ms .. 9 ms;
annex EMV2 {**

	properties
		AIAG_VDA::DFMEA =>[	FailureDescription =>"Invalid value from laser sensor";
							Function=>"Indicates the vehicle speed";
				 		 	PC=>"NONE";
							Occurrence=>6;
							DC=>"NONE";
							Detection=>7;] applies to  speed.OutOfRange; 
  		AIAG_VDA::DFMEA =>[ FailureDescription =>"Loss of speed from laser sensor";
		  		  			Function=>"Indicates the vehicle speed";
		  		 	 		PC=>"NONE";
							Occurrence=>6;
							DC=>"NONE";
							Detection=>7;] applies to  speed.ItemOmission;					 	
		AIAG_VDA::Optimization=>([	OptPC=>"Data redundancy";
		  							OptDC=>"Fault diagnosis";
									ResponsPerson=>"David";
									TargetCompletionData=>"2023/3/15";
									Status=>"in progress";
									OPTOccurrence=>3;
									OPTDetection=>4;]) applies to speed.OutOfRange;					 											 	
		AIAG_VDA::Optimization=>([	OptPC=>"Data redundancy";
		  							OptDC=>"Fault diagnosis";
									ResponsPerson=>"David";
									TargetCompletionData=>"2023/3/15";
									Status=>"in progress";
									OPTOccurrence=>3;
									OPTDetection=>4;]) applies to speed.ItemOmission;   								 			  				
**};
end speed_laser_sensor;


-------------
--  Brake  --
-------------

device brake 
features
	cmd : in data port aadlbook::icd::brake_cmd;
flows
	f0 : flow sink cmd;
properties
	Period => 2 ms; 
annex EMV2 {**
	use types 		ErrorLibrary;
	use behavior  	ErrorLibrary::FailStop;

	error propagations
		cmd : in propagation {ItemOmission,OutOfRange};
	flows
		ef0 : error sink cmd{ItemOmission,OutOfRange};
	end propagations;
	
	component error behavior
	events
		Reset : recover event;
	transitions
		t0 : Operational -[cmd{ItemOmission}]-> FailStop;
		t1 : Operational -[cmd{OutOfRange}]-> FailStop;
		t2 : FailStop -[Reset]-> Operational;
	end component;
	properties
		AIAG_VDA::DFMEA =>[ FailureDescription =>"Unable to brake due to abnormal brake command";
				  			Function=>"Brakes according to the brake command";
				  			Severity=>7;] applies to  FailStop; 
		EMV2::OccurrenceDistribution => [ ProbabilityValue => 0.5e-5 ; Distribution => Poisson;] applies to Failure;
**};
end brake;

device acceleration
features
	cmd : in data port aadlbook::icd::speed_cmd;
flows
	f0 : flow sink cmd;
properties
	Period => 2 ms;
annex EMV2 {** 
	use types 		ErrorLibrary;
	use behavior  	ErrorLibrary::FailStop;

	error propagations
		cmd : in propagation {ItemOmission,OutOfRange};
	flows
		ef0 : error sink cmd{ItemOmission,OutOfRange};
	end propagations;
	
	
	component error behavior
	events
		Reset : recover event;
	transitions
		t0 : Operational -[cmd{ItemOmission}]-> FailStop;
		t1 : Operational -[cmd{OutOfRange}]-> FailStop;
		t2 : FailStop -[Reset]-> Operational;
	end component;
	properties
		AIAG_VDA::DFMEA =>[ FailureDescription =>"Unable to accelerate due to abnormal acceleration command";
				  			Function=>"Accelerates according to the acceleration command";
				  			Severity=>7; ] applies to  FailStop; 
		AIAG_VDA::DFMEA =>[ FailureDescription =>"Invalid command from software";
		  		  			Function=>"Receive acceleration command";] applies to  cmd.OutOfRange; 
  		AIAG_VDA::DFMEA =>[ FailureDescription =>"Loss of acceleration command from software";
		  	      			Function=>"Receive acceleration command";] applies to  cmd.ItemOmission; 
		EMV2::OccurrenceDistribution => [ ProbabilityValue => 0.5e-5 ; Distribution => Poisson;] applies to Failure;
**};
end acceleration;

device panel
features
	increase_speed : out event port;
	decrease_speed : out event port;
flows
	f80 : flow source increase_speed;
annex EMV2 {**
	use types 		ErrorLibrary;
	use behavior  	ErrorLibrary::FailStop;

	error propagations
		increase_speed : out propagation {ItemOmission};
		decrease_speed : out propagation {ItemOmission};
	flows
		ef0 : error source increase_speed{ItemOmission};
		ef1 : error source decrease_speed{ItemOmission};
	end propagations;
	properties
		AIAG_VDA::DFMEA =>[ FailureDescription =>"Panel failure";
				  			Function=>"For increasing and decreasing the speed of the car";] applies to  FailStop; 
		AIAG_VDA::DFMEA =>[ FailureDescription =>"Loss of acceleration instruction";
		  					Function=>"Sends signals to the panel_controller";] applies to  increase_speed.ItemOmission; 
		AIAG_VDA::DFMEA =>[ FailureDescription =>"Loss of deceleration instructions";
		  					Function=>"Sends signals to the panel_controller";] applies to  decrease_speed.ItemOmission; 		  				
		EMV2::OccurrenceDistribution => [ ProbabilityValue => 0.5e-5 ; Distribution => Poisson;] applies to Failure;
		EMV2::OccurrenceDistribution => [ ProbabilityValue => 0.5e-5 ; Distribution => Poisson;] applies to ef0;
		EMV2::OccurrenceDistribution => [ ProbabilityValue => 0.5e-5 ; Distribution => Poisson;] applies to ef1;
**};
end panel;

device screen
features
	tire_pressure : in data port aadlbook::icd::pressure;
	desired_speed : in data port aadlbook::icd::speed;
	actual_speed : in data port aadlbook::icd::speed;
	warning : in data port aadlbook::icd::boolean;
	entertainment_infos : in data port aadlbook::icd::entertainment_infos;
flows
	f0 : flow sink warning;
annex EMV2 {**
	use types 		ErrorLibrary;
	use behavior  	ErrorLibrary::FailStop;

	error propagations
		tire_pressure : in propagation {OutOfRange};
		desired_speed : in propagation {ItemOmission,OutOfRange};
		actual_speed : in propagation {ItemOmission,OutOfRange};
		warning : in propagation {ItemOmission};
	flows
		ef0 : error sink tire_pressure{OutOfRange};
		ef1 : error sink desired_speed{ItemOmission};
		ef2 : error sink desired_speed{OutOfRange};
		ef3 : error sink actual_speed{ItemOmission};
		ef4 : error sink actual_speed{OutOfRange};
		ef5 : error sink warning{ItemOmission};
	end propagations;
	properties
		AIAG_VDA::DFMEA =>[ FailureDescription =>"Screen failure";
		  					Function=>"Display information on the dashboard";] applies to  FailStop; 
		EMV2::OccurrenceDistribution => [ ProbabilityValue => 0.5e-5 ; Distribution => Poisson;] applies to Failure;
**};
end screen;

device tpms
features
	pressure : out data port aadlbook::icd::pressure;
end tpms;

device bluetooth_controller
features
	bt        : requires bus access aadlbook::platform::bluetooth;
	music     : out data port aadlbook::icd::music.i;
	contacts  : out data port aadlbook::icd::contacts.i;
end bluetooth_controller;

device speaker
features
	music  : in data port aadlbook::icd::music.i;
flows
	f0 : flow sink music;
end speaker;

end aadlbook::devices;
